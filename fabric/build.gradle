plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
}

version = project.mod_version
group = project.mod_group

base {
    archivesName = project.archives_base_name
}

dependencies {
    // Terramap modules
    implementation project(":core")  // Shared implementation of Terramap
    implementation project(":smylib:core")  // Shared implementation of SmyLib
    implementation project(path: ":smylib:fabric", configuration: "namedElements")  // Fabric backend for SmyLib, https://fabricmc.net/wiki/documentation:fabric_loom

    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {

    def properties = [
            "version": project.version,
            "mod_id": project.mod_id,
            "mod_name": project.mod_name,
            "mod_author": project.mod_author,
            "fabric_loader_version": project.loader_version,
            "minecraft_version": project.minecraft_version,
            "java_version": project.java_version
    ]

    inputs.properties properties

    filesMatching("fabric.mod.json") {
        expand properties
    }
}